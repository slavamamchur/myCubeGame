apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }

    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28

        applicationId "com.sadgames.dicegame"
        versionCode 4
        versionName '1.0'
        versionNameSuffix '3'

        vectorDrawables.useSupportLibrary = true

        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/robovm/ios/robovm.xml'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }

    dexOptions {
        //incremental false
    }

    productFlavors {}
    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
    }

}

configurations { natives }

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    //compile 'com.google.firebase:firebase-appindexing:11.8.0'
    implementation 'com.android.support:support-v4:28.0.0'

    implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"

    implementation 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.3.2'
    implementation 'cz.advel.jbullet:jbullet:20101010-1'
    implementation 'org.luaj:luaj-jse:3.0.1'
    implementation 'org.sqldroid:sqldroid:1.0.3'

    implementation project(':GLES20Engine')
    implementation project(':AndroidSysUtils')
}

task copyAndroidNatives() {
    file("libs/armeabi-v7a/").mkdirs()
    file("libs/arm64-v8a/").mkdirs()
    file("libs/x86_64/").mkdirs()
    file("libs/x86/").mkdirs()

    configurations.natives.files.each { jar ->
        def outputDir = null
        if (jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
        if (jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
        if (jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
        if (jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
        if (outputDir != null) {
            copy {
                from zipTree(jar)
                into outputDir
                include "*.so"
            }
        }
    }
}
